plugins {
    id "java-gradle-plugin"
    id "com.gradle.plugin-publish" version "0.10.0"
    id 'org.ajoberstar.reckon' version "0.8.0"
    id 'com.github.johnrengelman.shadow' version '4.0.1'
}

group 'eu.xenit.gradle'

task wrapper(type: Wrapper) {
  gradleVersion = '4.8'
  distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

sourceCompatibility = 1.8

configurations {
    packaged
    compileOnly.extendsFrom(packaged)
    testCompile.extendsFrom(packaged)
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
    main {
        java {
            runtimeClasspath += configurations.packaged
        }
    }
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }

        resources.srcDir file('src/integration-test/resources')
    }
}


gradlePlugin {
    plugins {
        dockerAlfresco {
            id = "eu.xenit.docker-alfresco"
            implementationClass = "eu.xenit.gradle.alfresco.DockerAlfrescoPlugin"
        }
        dockerConfig {
            id = "eu.xenit.docker-config"
            implementationClass = "eu.xenit.gradle.docker.DockerConfigPlugin"
        }
        docker {
            id = "eu.xenit.docker"
            implementationClass = "eu.xenit.gradle.docker.DockerPlugin"
        }
    }
}

pluginBundle {
    vcsUrl = "https://github.com/xenit-eu/alfresco-docker-gradle-plugin"
    website = vcsUrl
    description = "A gradle plugin to create Alfresco docker images with extensions"
    tags = ["alfresco", "docker"]
    plugins {
        dockerAlfresco {
            displayName = "Alfresco docker plugin"
        }

        dockerConfig {
            displayName = "Docker configuration plugin"
        }

        docker {
            displayName = "Docker plugin"
        }
    }

    mavenCoordinates {
        groupId = "eu.xenit.gradle"
        artifactId = "alfresco-docker-plugin"
    }
}

reckon {
    scopeFromProp()
    snapshotFromProp()
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://artifacts.alfresco.com/nexus/content/groups/public/"
    }
}

dependencies {
    packaged 'org.alfresco:alfresco-mmt:5.2.g'
    compile 'com.bmuschko:gradle-docker-plugin:3.2.5'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile 'com.avast.gradle:gradle-docker-compose-plugin:0.8.8'
    compile group: 'org.apache.commons', name: 'commons-compress', version: '1.5'
    compile 'org.eclipse.jgit:org.eclipse.jgit:4.6.0.201612231935-r'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile gradleTestKit()
    testCompile "org.mockito:mockito-core:2.+"
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}

shadowJar {
    configurations = [project.configurations.packaged]
    classifier = "" // Change classifier so gradle plugin portal accepts our plugin
}

jar {
    // Changes classifier, because no classifier is already taken by shadowJar, and we don't want to overwrite
    classifier = "unbundled"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

configurations.archives.artifacts.clear() // Remove jar task output that is present by default

artifacts {
    add("archives", shadowJar)
    add("archives", sourcesJar)
    add("archives", javadocJar)
}

task integrationTest(type: Test, group: "verification") {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

check.dependsOn(integrationTest)
reckonTagCreate.dependsOn(check)

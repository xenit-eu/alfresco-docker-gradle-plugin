plugins {
    id "java-gradle-plugin"
    id "maven-publish"
    id "idea"
    id "jacoco"
    id "com.gradle.plugin-publish" version "0.12.0"
    id 'org.ajoberstar.reckon' version "0.12.0"
    id 'com.github.johnrengelman.shadow' version '6.0.0' apply false
    id "org.sonarqube" version "3.0"
    id "be.vbgn.ci-detect" version "0.3.0"
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

group 'eu.xenit.gradle'

sourceCompatibility = 1.8

configurations {
    packaged
    compileOnly.extendsFrom(packaged)
    testCompile.extendsFrom(packaged)
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
    main {
        java {
            runtimeClasspath += configurations.packaged
        }
    }
    integrationTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
}

idea {
    module {
        testSourceDirs += sourceSets.integrationTest.java.sourceDirectories
        testResourceDirs += sourceSets.integrationTest.resources.srcDirs
    }
}


gradlePlugin {
    plugins {
        dockerAlfresco {
            id = "eu.xenit.docker-alfresco"
            implementationClass = "eu.xenit.gradle.docker.alfresco.DockerAlfrescoPlugin"
        }
        dockerConfig {
            id = "eu.xenit.docker-config"
            implementationClass = "eu.xenit.gradle.docker.DockerConfigPlugin"
        }
        docker {
            id = "eu.xenit.docker"
            implementationClass = "eu.xenit.gradle.docker.DockerPlugin"
        }
        dockerCompose {
            id = "eu.xenit.docker-compose"
            implementationClass = "eu.xenit.gradle.docker.compose.DockerComposePlugin"
        }
        dockerComposeAuto {
            id = "eu.xenit.docker-compose.auto"
            implementationClass = "eu.xenit.gradle.docker.compose.DockerComposeAutoPlugin"
        }
    }
}

pluginBundle {
    vcsUrl = "https://github.com/xenit-eu/alfresco-docker-gradle-plugin"
    website = vcsUrl
    description = "A gradle plugin to create Alfresco docker images with extensions"
    tags = ["alfresco", "docker"]
    plugins {
        dockerAlfresco {
            displayName = "Alfresco docker plugin"
        }

        dockerConfig {
            displayName = "Docker configuration plugin"
        }

        docker {
            displayName = "Docker plugin"
        }

        dockerCompose {
            displayName = "docker-compose plugin"
        }

        dockerComposeAuto {
            displayName = "docker-compose plugin: automatic configuration for all projects"
        }
    }

    mavenCoordinates {
        groupId = "eu.xenit.gradle"
        artifactId = "alfresco-docker-plugin"
    }
}

reckon {
    scopeFromProp()
    snapshotFromProp()
}

repositories {
    mavenCentral()
    jcenter()
    gradlePluginPortal()
    maven {
        url "https://artifacts.alfresco.com/nexus/content/groups/public/"
    }
}


dependencies {
    packaged 'org.alfresco:alfresco-mmt:6.0'
    packaged group: 'commons-io', name: 'commons-io', version: '2.7'
    packaged('org.eclipse.jgit:org.eclipse.jgit:5.8.1.202007141445-r'){
        exclude group: 'org.slf4j'
    }

    compile 'com.bmuschko:gradle-docker-plugin:6.5.0'
    compile 'com.avast.gradle:gradle-docker-compose-plugin:0.13.0'
    testCompile group: 'junit', name: 'junit', version: '4.13'
    testCompile gradleTestKit()
    testCompile "org.mockito:mockito-core:2.+"
    testCompile 'com.github.stefanbirkner:system-rules:1.19.0'
}


task shadowJar(type: ShadowJar) {
    from sourceSets.main.output
    configurations = [project.configurations.packaged]
    archiveClassifier = "shadow"
    mergeServiceFiles()
    exclude "META-INF/*.SF"
    exclude "META-INF/*.RSA"
}

task configureShadowJar(type: ConfigureShadowRelocation) {
    target = shadowJar
    prefix = "eu.xenit.gradle.docker.internal.shadow"
}

shadowJar.dependsOn(configureShadowJar)

jar {
    from zipTree(shadowJar.outputs.files.singleFile)
    dependsOn(shadowJar)
}

import org.gradle.util.GradleVersion

task integrationTest(type: Test, group: "verification") {
    useJUnit()
    if (project.hasProperty("integrationTestGradleVersions")) {
        // This is used for sharding integration tests across different gradle versions
        // to avoid a timeout on travis because the total build takes too long.
        // See also the .travis.yml file
        project.property("integrationTestGradleVersions").tokenize(",").forEach({ version ->
            // The versions specified in `integrationTestGradleVersions` are major.minor.
            // We only want it to match those, and eventual patches to those versions.
            // In particular, we want 4.1 to match 4.1.2, but not 4.10.
            // Since there is only one test parameter for every version, either the first
            // pattern matches major.minor, or the second pattern matches major.minor.patch
            filter.includeTestsMatching("*[Gradle v${version}]")
            filter.includeTestsMatching("*[Gradle v${version}.*]")
        })
    }
    doFirst {
        if (gradle.startParameter.offline) {
            systemProperty "eu.xenit.gradle.integration.useGradleVersion", GradleVersion.current().version
        }
    }
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter(test)
}

jacocoTestReport {
    dependsOn(test, integrationTest)
    executionData(test, integrationTest)
    reports {
        xml.enabled = true
    }
}

sonarqube {
    properties {
        properties["sonar.tests"] += sourceSets.integrationTest.java.srcDirs
        if (ci.isPullRequest()) {
            properties["sonar.pullrequest.key"] = ci.pullRequest
            properties["sonar.pullrequest.branch"] = ci.branch
            properties["sonar.pullrequest.base"] = ci.pullRequestTargetBranch
        } else {
            properties["sonar.branch.name"] = ci.reference
        }
    }
}

check.dependsOn(integrationTest)
reckonTagCreate.dependsOn(check)
tasks.sonarqube.dependsOn(check, jacocoTestReport)

